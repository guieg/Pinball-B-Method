IMPLEMENTATION PinballI
REFINES Pinball
    
CONCRETE_CONSTANTS maxjogadores
    
PROPERTIES maxjogadores : NAT
    
VALUES 
    jogador = 0..7;
    nBolas = 5; 
    bumpers = 1..7;
    maxjogadores = 8;
    valorMaquina = 5;
    empty = 0;
    jogadores = 1..7
    
    
CONCRETE_VARIABLES
    jogadorAtual, bolasAtuais, pontuacaoAtual, estado, rankingi, cartaoi, recordsi, sequenciaAcertosi, fimRanking, fimSequencia
    
INVARIANT
    fimRanking : 0..(maxjogadores)
    & fimSequencia : 0..6 
    & jogadorAtual : jogador
    & bolasAtuais : NAT 
    & pontuacaoAtual : NAT 
    & estado : 0..5  
    & rankingi : jogador --> jogador
    & cartaoi : jogador --> NAT 
    & recordsi :  jogador --> NAT  
    & sequenciaAcertosi : 0..5 --> NAT
    
    
    
INITIALISATION
    jogadorAtual := empty; 
    bolasAtuais := 0; 
    pontuacaoAtual:= 0; 
    estado := 0;  
    cartaoi := jogador * {0}; 
    recordsi := jogador * {0};
    sequenciaAcertosi:= (0..5) * {0};
    fimRanking :=0;
    fimSequencia := 0;
    rankingi := jogador * {0}
    
    
OPERATIONS
    
    adicionarCredito(jj, cc) =
    BEGIN  cartaoi(jj) := cc + cartaoi(jj)
    END ;
    
    
    transferirCredito(j1, j2, cc) =  
    BEGIN cartaoi(j1) := cartaoi(j1) - cc;
        cartaoi(j2) := cartaoi(j2) + cc
    END ;
    
    iniciarPartida(jj) = 
    BEGIN jogadorAtual := jj;
        bolasAtuais := nBolas; 
        cartaoi(jj):= cartaoi(jj) - valorMaquina; 
        estado := 1; 
        pontuacaoAtual := 0
    END ;
    
    
    finalizar = 
    BEGIN
        estado := 2;
        bolasAtuais := 0;
        fimSequencia := 0;
             
        
        VAR ii, kk IN ii := 0; kk := rankingi(ii);
            WHILE (ii < fimRanking & kk /= jogadorAtual)
            DO 
                ii:= ii + 1; 
                IF ii < fimRanking THEN kk := rankingi(ii) END
            INVARIANT ii : 0..fimRanking & !i2 .(i2 : 0..(ii -1) => rankingi(i2) /= jogadorAtual)
            VARIANT fimRanking - ii
            END;
            
            IF ii = fimRanking THEN 
                rankingi(fimRanking) := jogadorAtual; fimRanking := fimRanking + 1
            END
        END;
        
        VAR ii IN ii:= recordsi(jogadorAtual);
            IF ii < pontuacaoAtual THEN 
                recordsi(jogadorAtual) := pontuacaoAtual
            END
        END;
        jogadorAtual:= empty
    END;
    
    perderBola = 
    BEGIN  
        bolasAtuais:= bolasAtuais -1; 
        fimSequencia := 0
    END;
    
    rk <-- posicaoRanking(pp)=
    BEGIN 
        VAR ii,kk IN ii := 0; kk := rankingi(ii);
            WHILE kk /= pp
            DO ii:= ii + 1; kk := rankingi(ii)
            INVARIANT ii < maxjogadores & #i2 .(i2 : ii..(maxjogadores -1) & rankingi(i2) = pp)
            VARIANT maxjogadores -1 - ii
            END;
            rk := ii
        END
    END
    
END